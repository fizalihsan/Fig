apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'cargo' //for auto-deployment to web container like tomcat
apply plugin: 'sonar-runner' //sonar server should be running to execute this.
apply plugin: 'groovy' //for Spock testing

//  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~ Configuration Properties ~~~~~~~~~~~~~~~~~~~~~~~~~~~~
sourceCompatibility = 1.7
version = '0.0.1'
final tomcatHome = "C:/apps/xampp/tomcat/"
final deployDir = "${tomcatHome}/webapps"
final cleanOldDeploymentCmd = "rm -rf ${deployDir}/Fig-${version}"

test{
    jvmArgs '-Xms64m', '-Xmx512m', '-XX:MaxPermSize=128m'
}

//  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~ Repositories  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.gradle.api.plugins:gradle-cargo-plugin:0.6.1'
    }
}

repositories {
    mavenCentral()
}

/*
task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}
*/

//  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~ Library dependencies ~~~~~~~~~~~~~~~~~~~~~~~~~~~~
final gsCollectionsVersion = "4.2.0"
final jettyVersion = '9.1.0.v20131115'
final jerseyVersion = '2.4'
final neo4jVersion = '1.9.5'

dependencies {
    //Collection Libraries
    compile "com.goldmansachs:gs-collections-api:${gsCollectionsVersion}",
            "com.goldmansachs:gs-collections:${gsCollectionsVersion}",
            "com.goldmansachs:gs-collections-forkjoin:${gsCollectionsVersion}"

    //Databinding Libraries
//    compile 'com.fasterxml.jackson.core:jackson-databind:2.2.3' //http://wiki.fasterxml.com/JacksonInFiveMinutes
    compile 'com.google.code.gson:gson:2.2.4' //https://sites.google.com/site/gson/gson-user-guide

    //Jetty server libraries
    compile "org.eclipse.jetty:jetty-project:${jettyVersion}",
            "org.eclipse.jetty:jetty-server:${jettyVersion}",
            "org.eclipse.jetty:jetty-servlet:${jettyVersion}",
            "org.eclipse.jetty:jetty-servlets:${jettyVersion}"

    //Logging frameworks
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.13'
    compile 'org.codehaus.groovy:groovy-all:2.1.9' //needed for logback groovy config

    //Neo4j Database
    //Revisit this later. We don't need all the neo4j libraries from top-level.
    compile "org.neo4j:neo4j:${neo4jVersion}",
            "org.neo4j:neo4j-shell:${neo4jVersion}"
    compile("org.neo4j.app:neo4j-server:${neo4jVersion}") {
        exclude group: 'janino' //pulls 2.5.10, but we need 2.6.1
        exclude group: 'ch.qos.logback'
    }
    compile "org.codehaus.janino:janino:2.6.1"

    //Webservice Libraries
    compile "javax.ws.rs:javax.ws.rs-api:2.0"
    compile "org.glassfish.jersey.containers:jersey-container-servlet:${jerseyVersion}"
    compile "org.glassfish.jersey.core:jersey-server:${jerseyVersion}"

    //Web Application Deployment
    cargo "org.codehaus.cargo:cargo-core-uberjar:1.3.3"
    cargo "org.codehaus.cargo:cargo-extensions-ant:1.3.3"

    //Unit testing libraries
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    testCompile group: "org.neo4j", name: "neo4j-kernel", version: "${neo4jVersion}", classifier: "tests"

    //YAML (Snakeyaml library)
    compile 'org.yaml:snakeyaml:1.13'
}



//  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~ Web Application Deployment ~~~~~~~~~~~~~~~~~~~~~~~~~~~~
task deployToTomcat(type: Copy) {
    dependsOn(clean, war)
    from war //The WAR task is aware of the artifacts it generates
    into deployDir
}

//Cargo plugin - https://github.com/bmuschko/gradle-cargo-plugin
cargo {
    containerId = 'tomcat7x' //List of supported containers - http://cargo.codehaus.org/Home
    port = 8080 //The TCP port the container responds on

    deployable { //this closure defines the deployable artifact.
//        file = file('xxxx.war') //artifact to deploy. Defaults to the project artifact
        context = 'fig' //The URL context the container is handling your web application on (defaults to WAR/EAR name)
    }

    local {
        homeDir = file("C:\\apps\\xampp\\tomcat\\")
        output = file('build/catalina.out')
    }
}


